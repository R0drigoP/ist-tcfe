*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 10.0
Vee vee 0 -10.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 ninv_in inv_in vcc vee out uA741

*High pass filter stage
C1 in ninv_in 220n
R1 0 ninv_in 1k

*Amplification
R2 0 inv_in 10k
R2p1 0 inv_in 10k
R2p2 0 inv_in 10k
R3 inv_in out 300k

*Low Pass Filter Stage
R4 out out2 0.5k
C2 out2 0 220n

* load
*RL out2 0 8

.op
.end

.control

print all

* time analysis
tran 1e-5 1e-2
*plot v(out2)
*hardcopy vo1.ps vdb(out)


* frequency analysis
ac dec 10 10 100MEG
plot vdb(out2) - vdb(in)
set units = degrees
*plot ph(out2)

hardcopy gain.ps vdb(out2)
echo gain_FIG

hardcopy phase.ps ph(out2)
echo phase_FIG
*=========================================================

let gain = vdb(out2)
let gainlinear = v(out2)/v(in)

meas ac gainfinal MAX gain from=100 to=100000
let allign=gainfinal-3
meas ac lowfrequency WHEN gain=allign RISE=1
meas ac highfrequency WHEN gain=allign FALL=1
let centerfrequency=sqrt(lowfrequency*highfrequency)

let gaindev = 100 - abs(v(out2)[20]/v(in)[20]); linear
let freqdev = abs(1000 - centerfrequency)

print gaindev
print freqdev

let zin = abs(v(in)[20]/vin#branch[20])

echo simval_TAB
print lowfrequency
print highfrequency
print centerfrequency
print gainfinal
echo simval_END

echo zin_TAB
print zin
echo zin_END

let cost_trans_opamp = 2*0.1
let cost_diode_opamp = 5*0.1
let cost_res_opamp = 100 + 2*5.305 + 2*1.836 + 13.19*1e3 + 0.05 + 0.01 + 18.16  
let cost_cap_opamp = (8.661 + 30)*1e-6
let cost_opamp = cost_cap_opamp + cost_res_opamp + cost_diode_opamp + cost_trans_opamp
let cost_res = 3*100 + 3*10 + 3*1 
let cost_cap = 2*220*1e-3
let cost = cost_opamp + cost_res + cost_cap

let merit = 1/(cost*(gaindev*freqdev + 1e-6))

echo merit_TAB
print cost
print merit
echo merit_END

.endc 

