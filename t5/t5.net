*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 ninv_in inv_in vcc vee out uA741

*High pass filter stage
C1 in ninv_in 220n
R1 0 ninv_in 10k
R1p 0 ninv_in 1k

*Amplification
R2 0 inv_in 10k
R2p 0 inv_in 1k
R3 inv_in s3 100k
R3s s3 out 100k

*Low Pass Filter Stage
R4 out out2 0
C2 out2 0 220n

* load
RL out2 0 8

.op
.end

.control

print all

* time analysis
tran 1e-5 1e-2
*plot v(out2)
*hardcopy vo1.ps vdb(out)


* frequency analysis
ac dec 10 10 100MEG
plot vdb(out2) - vdb(in)
*plot vp(out2)
*hardcopy vo1f.ps vdb(out)

*input impedance in kohm
*print v(in)[40]/R1[i]/1000

*=========================================================

let gain = vdb(out2)
let gainlinear = v(out2)/v(in)

let inzimpedance = v(in)[40]/vin#branch[40]*(-1)

meas ac maxout MAX vdb(out2) from=100 to=100000
meas ac gainfinal MAX gain from=100 to=100000
let allign=gainfinal-3
meas ac lowfrequence WHEN gain=allign RISE=1
meas ac highfrequence WHEN gain=allign FALL=1
let centerfrequency1=lowfrequence+(highfrequence-lowfrequence)/2
let centerfrequency=sqrt(lowfrequence*highfrequence)
meas ac fmax WHEN gain=gainfinal

meas ac gainfinallinear MAX gainlinear from=100 to=100000

echo "valsim_TAB"
print lowfrequence
print highfrequence
print centerfrequency
print gainfinal
echo "valsim_END"


.endc 

